import com.liferay.gradle.util.copy.StripPathSegmentsAction

apply plugin: "com.liferay.gulp"
apply plugin: "com.liferay.lang.merger"

task testJS
task buildProtobufjs(type: Copy)
task wrapProtobufjs

configurations {
	protobufjs
}

String protobufjsVersion = "6.7.3"

buildProtobufjs {
	eachFile new StripPathSegmentsAction(6)

	from {
		zipTree(configurations.protobufjs.singleFile)
	}

	include "META-INF/resources/webjars/protobufjs/${protobufjsVersion}/dist/protobuf.js"
	includeEmptyDirs = false
	into "classes/META-INF/resources/js"
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

configJSModules {
	dependsOn = []
	enabled = false
}

dependencies {
	compileInclude group: "com.google.protobuf", name: "protobuf-java", version: "3.2.0"

	protobufjs group: "org.webjars.npm", name: "protobufjs", transitive: false, version: protobufjsVersion

	provided group: "biz.aQute.bnd", name: "biz.aQute.bndlib", version: "3.1.0"
	provided group: "com.liferay", name: "com.liferay.dynamic.data.mapping.io", version: "2.0.0"
	provided group: "com.liferay.portal", name: "com.liferay.portal.impl", version: "2.0.0"
	provided group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "2.27.0-20170422.064536-3"
	provided group: "javax.portlet", name: "portlet-api", version: "2.0"
	provided group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	provided group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"
	provided project(":apps:forms-and-workflow:dynamic-data-mapping:dynamic-data-mapping-api")
	provided project(":apps:forms-and-workflow:dynamic-data-mapping:dynamic-data-mapping-form-evaluator")
	provided project(":apps:forms-and-workflow:dynamic-data-mapping:dynamic-data-mapping-form-field-type")

	testCompile group: "com.liferay", name: "com.liferay.dynamic.data.mapping.test.util", version: "2.0.0"
	testCompile group: "com.liferay", name: "com.liferay.osgi.service.tracker.collections", version: "2.0.0"
	testCompile group: "com.liferay", name: "com.liferay.registry.api", version: "1.0.0"
	testCompile group: "org.jabsorb", name: "jabsorb", version: "1.3.1"
	testCompile group: "org.jodd", name: "jodd-bean", version: "3.6.4"
	testCompile group: "org.jodd", name: "jodd-core", version: "3.6.4"
	testCompile group: "org.jodd", name: "jodd-json", version: "3.6.4"
	testCompile group: "org.slf4j", name: "slf4j-api", version: "1.7.22"
	testCompile project(":apps:forms-and-workflow:dynamic-data-mapping:dynamic-data-mapping-data-provider")
	testCompile project(":apps:forms-and-workflow:dynamic-data-mapping:dynamic-data-mapping-expression")
	testCompile project(":apps:forms-and-workflow:dynamic-data-mapping:dynamic-data-mapping-form-evaluator-impl")
	testCompile project(":apps:forms-and-workflow:dynamic-data-mapping:dynamic-data-mapping-service")
}

classes {
	dependsOn "wrapProtobufjs"
}

testJS {
	dependsOn gulpTest
}

transpileJS {
	soySrcIncludes = ""
	srcIncludes = "**/*.es.js"
}

wrapProtobufjs {
	dependsOn buildProtobufjs

	doLast {
		File protobufJsFile = file("classes/META-INF/resources/js/protobuf.js")

		String wrapperHeader = "AUI.add('liferay-ddm-protobufjs', function(A) {"
		String wrapperFooter = "}, '', {requires: []});";

		protobufJsFile.text = wrapperHeader + protobufJsFile.text.replace("(typeof window===\"object\"&&window||typeof self===\"object\"&&self||this)", "(Liferay.DDM)") + wrapperFooter
	}
}

wrapSoyAlloyTemplate {
	enabled = true
	moduleName = "liferay-ddm-form-soy"
	namespace = "ddm"
}